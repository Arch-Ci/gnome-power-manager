AC_PREREQ(2.52)

AC_INIT(gnome-power-manager, 0.2.7.2)
AC_CONFIG_SRCDIR(src)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_INSTALL
AM_PROG_LIBTOOL

ALL_LINGUAS="de en_CA en_GB es fr it mk nb no pa pl sv uk zh_CN"
AC_SUBST(ALL_LINGUAS)

AC_DEFINE(GETTEXT_PACKAGE, "AC_PACKAGE_NAME", [foo])
GETTEXT_PACKAGE=AC_PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)

AC_PROG_INTLTOOL([0.27.2])
AM_GLIB_GNU_GETTEXT

AC_SUBST(SYSCONFDIR, $sysconfdir)
AC_SUBST(DATADIR, $datadir)
AC_SUBST(BINDIR, $bindir)
AC_SUBST(SBINDIR, $sbindir)

AC_ARG_ENABLE(libnotify,          [  --enable-libnotify      build with libnotify support],enable_libnotify=$enableval,enable_libnotify=auto)
AC_ARG_ENABLE(doxygen-docs,       [  --enable-doxygen-docs   build DOXYGEN documentation (requires Doxygen)],enable_doxygen_docs=$enableval,enable_doxygen_docs=auto)
AC_ARG_WITH(dbus-sys,             [  --with-dbus-sys=<dir>   where D-BUS system.d directory is])
AC_ARG_WITH(dbus-services,        [  --with-dbus-services=<dir>   where D-BUS services directory is])

# documentation target
AC_ARG_WITH(doc-dir,   [  --with-doc-dir=[dirname] directory to install documentation])
if ! test -z "$with_doc_dir"; then
   DOCDIR="$with_doc_dir/gnome-power-manager-$VERSION"
else
   DOCDIR="$DATADIR/doc/gnome-power-manager-$VERSION"
fi
AC_SUBST(DOCDIR)

# dbus system
if ! test -z "$with_dbus_sys" ; then
	DBUS_SYS_DIR="$with_dbus_sys"
else
	DBUS_SYS_DIR="$SYSCONFDIR/dbus-1/system.d"
fi

# dbus services
if ! test -z "$with_dbus_services" ; then
	DBUS_SERVICES_DIR="$with_dbus_services"
else
	# D-BUS 0.23 and higher use $prefix/share/dbus-1/services
	DBUS_SERVICES_DIR="$DATADIR/dbus-1/services"
fi

### Doxygen Documentation

AC_PATH_PROG(DOXYGEN, doxygen, no)

AC_MSG_CHECKING([whether to build Doxygen documentation])

if test x$DOXYGEN = xno ; then
    have_doxygen=no
else
    have_doxygen=yes
fi

if test x$enable_doxygen_docs = xauto ; then
    if test x$have_doxygen = xno ; then
        enable_doxygen_docs=no
    else
        enable_doxygen_docs=yes
    fi
fi

if test x$enable_doxygen_docs = xyes; then
    if test x$have_doxygen = xno; then
	AC_MSG_ERROR([Building Doxygen docs explicitly required, but Doxygen not found])
    fi
fi

AM_CONDITIONAL(DOXYGEN_DOCS_ENABLED, test x$enable_doxygen_docs = xyes)
AC_MSG_RESULT(yes)

AC_SUBST(DBUS_SYS_DIR)
AC_SUBST(DBUS_SERVICES_DIR)
AC_DEFINE_UNQUOTED(DBUS_SYSTEMD_DIR, "$DBUS_SYS_DIR", [Where system.d dir for DBUS is])
AC_DEFINE_UNQUOTED(DBUS_SERVICES_DIR, "$DBUS_SERVICES_DIR", [Where services dir for DBUS is])

PKG_CHECK_MODULES(GLIB, glib-2.0 gobject-2.0)
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

PKG_CHECK_MODULES(HAL, hal >= 0.5.4)
AC_SUBST(HAL_CFLAGS)
AC_SUBST(HAL_LIBS)

PKG_CHECK_MODULES(DBUS, dbus-glib-1 >= 0.35.2 dbus-1 >= 0.35.2 gthread-2.0)
AC_SUBST(DBUS_CFLAGS)
AC_SUBST(DBUS_LIBS)

PKG_CHECK_MODULES(GNOME, libgnomeui-2.0 libglade-2.0 libwnck-1.0 >= 2.1.5 gtk+-2.0 >= 2.6.0)
AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOME_LIBS)

AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2

CFLAGS="$CFLAGS -Wall -Wfloat-equal -Wundef -Wpointer-arith -Wbad-function-cast -Wredundant-decls"

# libnotify detection
if test x$enable_libnotify = xno ; then
    have_libnotify=no;
else
    # See if we have sufficiently new libnotify library
    PKG_CHECK_MODULES(LIBNOTIFY, libnotify >= 0.2.1, have_libnotify=yes, have_libnotify=no)
    if test x$enable_libnotify = xauto ; then
        if test x$have_libnotify = xno ; then
                AC_MSG_WARN([Sufficiently new libnotify library not found])
        fi
    else 
        if test x$have_libnotify = xno ; then
                AC_MSG_ERROR([libnotify explicitly required, and sufficiently new libnotify library not found])
        fi
    fi
fi
AM_CONDITIONAL(HAVE_LIBNOTIFY, test x$have_libnotify = xyes)
if test x$have_libnotify = xyes ; then
    AC_DEFINE(HAVE_LIBNOTIFY,1,[libnotify support])
fi

AC_PATH_PROG(DOCBOOK2MAN, docbook2man, no)
if test "$DOCBOOK2MAN" = "no" ; then
	AC_MSG_WARN([docbook2man not found, will not be able to build man documentation])
	fi
AM_CONDITIONAL(HAVE_DOCBOOK2MAN, [test "$DOCBOOK2MAN" != "no"])

AC_OUTPUT([
Makefile
src/Makefile
src/pixmaps/Makefile
src/icons/Makefile
docs/Makefile
docs/Doxyfile
po/Makefile.in
gnome-power-manager.spec
])

dnl ==========================================================================
echo "
                    GNOME Power Manager $VERSION
                  =============================

        prefix:                    ${prefix}
        compiler:                  ${CC}
        cflags:                    ${CFLAGS}
        libnotify support:         ${have_libnotify}
        building Doxygen docs:     ${enable_doxygen_docs}
        documentation dir:         $DOCDIR
        dbus-1 system.d dir:       $DBUS_SYS_DIR
        dbus-1 services dir:       $DBUS_SERVICES_DIR
	gconf-schema dir:          $GCONF_SCHEMA_FILE_DIR
"
