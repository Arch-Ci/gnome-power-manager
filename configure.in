AC_PREREQ(2.52)

AC_INIT(gnome-power-manager, 0.1.5)
AC_CONFIG_SRCDIR(src)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_INSTALL
AM_PROG_LIBTOOL

ALL_LINGUAS="de en_CA en_GB es fr mk nb no pa sv uk zh_CN"
AC_SUBST(ALL_LINGUAS)

AC_DEFINE(GETTEXT_PACKAGE, "AC_PACKAGE_NAME", [foo])
GETTEXT_PACKAGE=AC_PACKAGE_NAME
AC_SUBST(GETTEXT_PACKAGE)

AC_PROG_INTLTOOL([0.27.2])
AM_GLIB_GNU_GETTEXT

AC_ARG_ENABLE(libnotify,          [  --enable-libnotify      build with libnotify support],enable_libnotify=$enableval,enable_libnotify=auto)
AC_ARG_ENABLE(gscreensaver,       [  --enable-gscreensaver   build with gnome-screensaver support],enable_gscreensaver=$enableval,enable_gscreensaver=auto)
AC_ARG_WITH(dbus-sys,             [  --with-dbus-sys=<dir>   where D-BUS system.d directory is])
AC_ARG_WITH(dbus-services,        [  --with-dbus-services=<dir>   where D-BUS services directory is])

SYSCONFDIR="$sysconfdir"
LIBDIR="$libdir"
DATADIR="$datadir"

if ! test -z "$with_dbus_sys" ; then
	DBUS_SYS_DIR="$with_dbus_sys"
else
	DBUS_SYS_DIR="$SYSCONFDIR/dbus-1/system.d"
fi

if ! test -z "$with_dbus_services" ; then
	DBUS_SERVICES_DIR="$with_dbus_services"
else
	# D-BUS 0.23 and higher use $prefix/share/dbus-1/services
	DBUS_SERVICES_DIR="$DATADIR/dbus-1/services"
fi

AC_SUBST(DBUS_SYS_DIR)
AC_SUBST(DBUS_SERVICES_DIR)
AC_DEFINE_UNQUOTED(DBUS_SYSTEMD_DIR, "$DBUS_SYS_DIR", [Where system.d dir for DBUS is])
AC_DEFINE_UNQUOTED(DBUS_SERVICES_DIR, "$DBUS_SERVICES_DIR", [Where services dir for DBUS is])

PKG_CHECK_MODULES(GLIB, glib-2.0 gobject-2.0)
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

PKG_CHECK_MODULES(HAL, hal >= 0.5.3)
AC_SUBST(HAL_CFLAGS)
AC_SUBST(HAL_LIBS)

PKG_CHECK_MODULES(DBUS, dbus-glib-1 >= 0.35.2 dbus-1 >= 0.35.2 gthread-2.0)
AC_SUBST(DBUS_CFLAGS)
AC_SUBST(DBUS_LIBS)

PKG_CHECK_MODULES(GNOME, libgnomeui-2.0 libglade-2.0 libwnck-1.0 >= 2.1.5 gtk+-2.0 >= 2.6.0)
AC_SUBST(GNOME_CFLAGS)
AC_SUBST(GNOME_LIBS)

AC_PATH_PROG(GCONFTOOL, gconftool-2)
AM_GCONF_SOURCE_2

CPPFLAGS="$CPPFLAGS -ansi -Wall"
LDFLAGS="$LDFLAGS -ansi -Wall"

# libnotify detection
if test x$enable_libnotify = xno ; then
    have_libnotify=no;
else
    # See if we have sufficiently new libnotify library
    PKG_CHECK_MODULES(LIBNOTIFY, libnotify >= 0.2.1, have_libnotify=yes, have_libnotify=no)
    if test x$enable_libnotify = xauto ; then
        if test x$have_libnotify = xno ; then
                AC_MSG_WARN([Sufficiently new libnotify library not found])
        fi
    else 
        if test x$have_libnotify = xno ; then
                AC_MSG_ERROR([libnotify explicitly required, and sufficiently new libnotify library not found])
        fi
    fi
fi
AM_CONDITIONAL(HAVE_LIBNOTIFY, test x$have_libnotify = xyes)
if test x$have_libnotify = xyes ; then
    AC_DEFINE(HAVE_LIBNOTIFY,1,[libnotify support])
fi

# gnome-screensaver detection
if test x$enable_gscreensaver = xno ; then
    have_gscreensaver=no;
else
    # See if we have sufficiently new gscreensaver library
    # TODO - How do we test for gnome-screensaver?
    PKG_CHECK_MODULES(GSCREENSAVER, gscreensaver >= 0.0.1, have_gscreensaver=yes, have_gscreensaver=no)
    if test x$enable_gscreensaver = xauto ; then
        if test x$have_gscreensaver = xno ; then
                AC_MSG_WARN([Sufficiently new gnome-screensaver library not found])
        fi
    else 
        if test x$have_gscreensaver = xno ; then
                AC_MSG_ERROR([gnome-screensaver explicitly required, but gnome-screensaver not found])
        fi
    fi
fi
AM_CONDITIONAL(HAVE_GSCREENSAVER, test x$have_gscreensaver = xyes)
if test x$have_gscreensaver = xyes ; then
    AC_DEFINE(HAVE_GSCREENSAVER,1,[gscreensaver support])
fi

AC_OUTPUT([
Makefile
src/Makefile
src/pixmaps/Makefile
src/sounds/Makefile
src/icons/Makefile
po/Makefile.in
gnome-power-manager.spec
])

dnl ==========================================================================
echo "
                    GNOME Power Manager $VERSION
                  =============================

        prefix:                    ${prefix}
        compiler:                  ${CC}
        cflags:                    ${CFLAGS}
        cppflags:                  ${CPPFLAGS}
        libnotify support:         ${have_libnotify}
        gnome-screensaver support: ${have_gscreensaver}
        dbus-1 system.d dir:       $DBUS_SYS_DIR
        dbus-1 services dir:       $DBUS_SERVICES_DIR
	gconf-schema dir:          $GCONF_SCHEMA_FILE_DIR
"
